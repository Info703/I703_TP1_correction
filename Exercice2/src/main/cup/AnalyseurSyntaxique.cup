/* package et imports */
package fr.usmb.m1isc.compilation.tp1;
import java_cup.runtime.Symbol;
import java.util.HashMap;

/* inclusion du code */

action code {: 
    // pour utilisation dans les actions (classe action)
	// HashMap pour la gestion des variables
	private HashMap<String,Integer> vars = new HashMap<>();
:};

 
parser code {:
    // pour le parser (redefinition de la methode reportant les erreurs d'analyse)
:};

 init with {:
    //	initialisation du parser
:};

/* symboles terminaux */
terminal PLUS, MOINS, MUL, DIV, PAR_G, PAR_D, SEMI, LET, EGAL, ERROR; 
terminal Integer ENTIER;
terminal String IDENT;
/* non terminaux */
non terminal liste_expr, expr;
non terminal Integer expression, affectation ;

precedence left PLUS, MOINS;
precedence left MUL, DIV;

/* grammaire */
// on a une liste d'expressions (avec au moins une expression)
liste_expr	::= expr liste_expr 
			  | expr  
			  ;
// chaque expression arithmetique est terminee par un point virgule (SEMI)
expr 		::= expression:e SEMI   {: System.out.println("val: "+e); :}
			  | affectation:v SEMI  {: System.out.println("val: "+v); :}
			  | error SEMI
			  ;
			  
affectation ::= LET IDENT:nom EGAL expression:e 	{: vars.put(nom,e); RESULT = e; :}
              ;

expression 	::= ENTIER:e                            {: RESULT = e ; :}
			  | expression:e1 PLUS expression:e2    {: RESULT = e1+e2 ; :}
			  | expression:e1 MOINS expression:e2   {: RESULT = e1-e2 ; :}
			  | expression:e1 MUL expression:e2     {: RESULT = e1*e2 ; :}
			  | expression:e1 DIV expression:e2     {: RESULT = e1/e2 ; :}
			  | PAR_G expression:e PAR_D            {: RESULT = e ; :}
			  | IDENT:nom                          	{: RESULT = vars.get(nom) ; :}
			  ;





